plugins {
    id 'groovy-gradle-plugin'
    id "com.diffplug.spotless" version "7.0.1"
}

repositories {
    gradlePluginPortal()
}

dependencies {
    implementation("com.diffplug.spotless:spotless-plugin-gradle:7.0.1")
    implementation("io.freefair.gradle:lombok-plugin:8.11")
}

tasks.withType(JavaCompile).configureEach {
    options.warnings = true
    options.deprecation = true
    options.compilerArgs << "-Xlint:unchecked"
    dependsOn("spotlessApply")
}

spotless {
    encoding 'UTF-8'
    format 'misc', {
        target 'gradle/wrapper/gradle-wrapper.properties', '.gitattributes', '.gitignore', 'gradlew', 'gradlew.bat'
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target '*.gradle' // default target of groovyGradle
        greclipse().configFile('../greclipse.properties')
        importOrder()
        trimTrailingWhitespace()
        endWithNewline()
    }
    flexmark {
        target '**/*.md'
        flexmark()
    }
    format 'prettier', {
        target '.github/**/*.yml', '.vscode/**/*.json', '**/*.json', '**/*.html', '**/*.xml', '**/*.yaml', '**/*.yml', '.prettierrc'
        prettier(['prettier': '3.4.2', '@prettier/plugin-xml': '3.4.1']).npmInstallCache().config(['printWidth': 120,
            "xmlSelfClosingSpace": false,
            "xmlSortAttributesByKey": true,
            "xmlWhitespaceSensitivity": "ignore",
            "plugins": ["@prettier/plugin-xml"]
        ])
        trimTrailingWhitespace()
        endWithNewline()
    }
}
